---
title: Hakyll
tags: haskell, hakyll
---

This post runs through the process of setting up my blog using Hakyll. We'll cover some of the customisations I adopted and modified.

<!--more-->

You may have noticed that this blog is proudly powered by Hakyll. [Many others](https://jaspervdj.be/hakyll/examples.html) have posted about setting up or transitioning to a Hakyll based blog, but I thought I'd recount my experience anyway. I'm a novice with Haskell, so forgive me if some of the following isn't quite the Haskell way. You can find the source code for my Hakyll blog at [https://github.com/keithschulze/keithschulze.github.io/]

I won't cover setting up RSS because there are other posts and an official tutorial on the Hakyll site that cover this.

## NPM, Bootstrap and lesscss
I wanted to use NPM/Yarn to manage Bootstrap and other frontend dependencies. I also wanted to use LESS to build CSS. Much of the following is poached directly from [https://meta.plasm.us](https://meta.plasm.us)--Thanks Travis.

```haskell
match (fromList $ lessFiles defaultEngineConfig) $ do
    route $ setExtension "css"
    compile $ getResourceString
        >>= withItemBody (unixFilter (lessCommand defaultEngineConfig) $ "-" : (lessOptions defaultEngineConfig))
        >>= return . fmap compressCss
```

## Rendering tags a little differently

The Hakyll website has really nice instructions on how to add tags to a Hakyll blog and how to generate pages that aggregate aritcles containing a tag. Those instructions were pretty straight forward and all I wanted was to style things a little differently. Rather than render the tags as a series of comma-separated links, I wanted to render them as dark coloured tokens instead.

```haskell
-- Custom tag rendering
tagsFld :: String -> Tags -> Context a
tagsFld = tagsFieldWith getTags tagRenderLink (mconcat . intersperse "")

-- Render one tag link
tagRenderLink :: String -> (Maybe FilePath) -> Maybe H.Html
tagRenderLink _   Nothing         = Nothing
tagRenderLink tag (Just filePath) =
  Just $ H.a ! A.href (toValue $ toUrl filePath) ! A.id "tag" $ toHtml tag
```

## Bootstrapifying table, img & blockquote

Bootstrap provides support for nicely styling HTML tables and block-quotes; however, one needs to add the appropriate CSS class attributes to the `table` and `blockquote` HTML elements. Initially I thought I could post process the HTML produced by the `pandocCompiler` using RegExp. Knowing nothing about Regexps in Haskell

```haskell
processTables :: String -> String
processTables i =
  replaceAll "\n<table(.|\n)+</table>\n" processTable i

processTable :: String -> String
processTable ts
  | [_, attr, body]:_ <- ts =~ ("\n<table(.*)>(.+)</table>\n" :: String) :: [[String]] =
    "\n<div class=\"table-responsive-md\">\n<table " ++ attr ++ " class=\"table table-striped table-dark\">" ++ body ++ "</table>\n</div>\n"
  | otherwise = ts
```

I pretty soon realised that this wasn't going to handle multiple tables well. Overall, it'd be messy trying to come up with complex Regex rules to handle corner cases. Then an intuition came while I was browsing the Hakyll source and saw the `relativizeUrls` function, which uses `tagsoup` to parse HTML and process each tag. Adding the `table table-striped table-dark` class attributes to each table was dead easy:

```haskell
withBootstrapTables :: String -> String
withBootstrapTables = withTags tag
    where
        tag (TS.TagOpen "table" attrs) = TS.TagOpen "table" (attrs ++ [("class","table table-striped table-dark")])
        tag x = x
```

Note that `TS` in the code block above is a qualified import of TagSoup i.e., `import qualified Text.HTML.TagSoup as TS`.
This maps over each tag using `withTags :: (TS.Tag String -> TS.Tag String) -> String -> String`, matches the `TS.TagOpen` tags with a name "table" and adds the `table table-striped table-dark` class attribute.

Making tables responsive is a slightly more tricky problem because it requires the `table` element to be wrapped in a `div` with the `table-responsive-lg` class attribute, which isn't possible using the `withTags` function. Turns out Hakyll recently (as of [version 4.12.0.0](https://github.com/jaspervdj/hakyll/blob/v4.12.0.0/CHANGELOG.md#hakyll-41200)) added a related function `withTagList :: ([TS.Tag String] -> [TS.Tag String]) -> String -> String`. In this case, we need to provide a function that takes as input a list of all the Tags and returns a list of Tags. The nice thing about this is that we can fold over the list as opposed to map, which means we have an oppotunity to add extra elements if need be.

Tags can also be enhanced with Bootstrap and some custom CSS. You should be able to see a little preview of what they look like to the right.

> In the long history of humankind (and animal kind, too) those who learned to collaborate and improvise most effectively have prevailed.
> <footer class="blockquote-footer">Charles Darwin</footer>

Here we make use of the `withTags` function.

```haskell
withBootstrapQuotes :: String -> String
withBootstrapQuotes = withTags tag
    where
        tag (TS.TagOpen "blockquote" attrs) = TS.TagOpen "blockquote" (attrs ++ [("class","blockquote text-right")])
        tag x = x
```

We can also make images Bootstrap using the `.img-fluid` class. One wrinkle is that Pandoc's markdown supports adding attributes to `img` tags, which could be useful for adjusting `img` properties in special cases. The problem is that the `.img-fluid` class attribute can interfere with other custom attributes introduced via Pandoc. So in the case where other class attributes are already on the `img` tag, we'd like to avoid introducing the `.img-fluid` class attribute.

```haskell
withBootstrapImgs :: String -> String
withBootstrapImgs = withTags tag
    where
        tag (TS.TagOpen "img" attrs) =
            if elem "class" (fst $ unzip attrs)
            then TS.TagOpen "img" attrs
            else TS.TagOpen "img" (attrs ++ [("class", "img-fluid")])
        tag x = x
```

## Drafts
While I haven't done any real blogging in the past, my writing process typically means I have multiple posts in a draft form before I actually finish one. I wanted the ability to support having all these drafts going without the *risk* of publishing some unfinished/clarified material. Hence, I really wanted to have 2 directories of content in the source repositoryâ€”one for drafts that doesn't get published to the site and another for published posts.

There are a number of other blogs[^1]^,^[^2] that describe drafting setups for Hakyll. I opted for a hybrid setup where the Hakyll config is adjusted when Hakyll is in watch mode. This means that I can preview the drafts using Hakyll `watch`, but Hakyll `build` ignores them.


The output directories of a Hakyll build are defined by a `Hakyll.Core.Configuration`. By default, Hakyll uses `defaultConfiguration` in the `Hakyll.Core.Configuration` module. This is fine for building the site. What we want to do is configure a different set of directories for the `watch` builds.

```haskell
-- Get the action command by extracting the first items from arguments list
(action:_) <- getArgs

-- If the first argument is watch we should enable preview mode, where the
-- Hakyll configuration is adjust to use different build directories and
-- to use a pattern that matches both the 'drafts' and 'posts' dir.
let preview = action == "watch"
    clean = action == "clean"
    hakyllConf' =
      if preview
        then hakyllConf
          { destinationDirectory = "_watch"
          , storeDirectory = "_watchCache"
          , tmpDirectory = "_watchCache/tmp"
          }
        else hakyllConf
    postsPattern =
      if preview
        then "posts/*" .||. "drafts/*"
        else "posts/*"
```

[^1]: <https://www.blaenkdenum.com/posts/drafts-in-hakyll/>
[^2]: <http://limansky.me/posts/2015-10-31-Draft-posts-with-Hakyll.html>